?tacl routine

==  Copyright (c) 2016 NuWave Technologies, Inc. All rights reserved. 

#frame

#push pathmonName
#push console
#push volumes

== Customize these parameters for your system
#set console $zhome
#set pathmonName $nwemp
== End of customization

#output Using Pathmon name [pathmonName]

==  Shutdown the existing pathmon
[#if [#PROCESSEXISTS [pathmonName]] |then|
  #output Shutting down Pathmon [pathmonName] ...
  pathcom [pathmonName] ; shutdown2, mode immediate
  sink / [#stop [pathmonName]] /
]

==  Start the pathmon and give it a few moments to start.
assign pathctl, empctl

#output Starting pathmon [pathmonName] ...
pathmon / name [pathmonName], &
in [console], &
out [console], &
term [console], &
nowait /
clear assign pathctl

#delay 500

==  Configure the Pathway.

#output Configuring pathmon [pathmonName] ...

pathcom /inline/ [pathmonName]
inlprefix +
+ errors 99
+ set pathway maxassigns        50
+ set pathway maxdefines        50
+ set pathway maxexternaltcps   0
+ set pathway maxlinkmons       16
+ set pathway maxparams         20
+ set pathway maxpathcoms       4
+ set pathway maxprograms       10
+ set pathway maxserverclasses  8
+ set pathway maxserverprocesses 40
+ set pathway maxspi            4
+ set pathway maxstartups       4
+ set pathway maxtcps           2
+ set pathway maxtellqueue      0
+ set pathway maxtells          0
+ set pathway maxterms          10
+ set pathway maxtmfrestarts    0
+ set pathway security          "n"
+ start pathway cold !

+ reset server
+ set server cpus           0:1
+ set server createdelay    0 secs
+ set server deletedelay    10 mins
+ set server highpin        on
+ set server linkdepth      5
+ set server maxservers     5
+ set server maxlinks       20
+ set server numstatic      1
+ set server program        empsvr
+ set server tmf            on
+ set server debug          off
+ set server in             [console]
+ set server out            [console]
+ set server param          pathway "1"
+ add server employee-server

+ start server *

inleof

#unframe
